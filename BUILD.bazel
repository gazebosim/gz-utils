load(
    "@ignition//tools/workspace:build_defs.bzl",
    "IGNITION_FEATURES",
    "IGNITION_VISIBILITY",
    "ign_configure_header",
    "ign_export_header",
    "ign_include_header",
)
load(
    "@ignition//tools/lint:lint.bzl",
    "add_lint_tests",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = IGNITION_FEATURES,
)

licenses(["notice"])  # Apache-2.0

ign_configure_header(
    name = "config",
    src = "include/ignition/utils/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    package = "ign_utils",
)

ign_export_header(
    name = "include/ignition/utils/Export.hh",
    export_base = "IGNITION_UTILS",
    lib_name = "ignition-utils",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/ignition/utils/*.hh",
    "include/ignition/utils/detail/*.hh",
])

ign_include_header(
    name = "utilshh_genrule",
    out = "include/ignition/utils.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/utils/config.hh",
        "include/ignition/utils/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/utils/config.hh",
    "include/ignition/utils/Export.hh",
    "include/ignition/utils.hh",
]

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(
    ["src/*_TEST.cc"],
)

cc_library(
    name = "ign_utils",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
)

# Tests
[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":ign_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]

cc_library(
    name = "implptr_test_classes",
    srcs = [
        "test/integration/implptr/implptr_test_classes.cc",
    ],
    hdrs = [
        "test/integration/implptr/implptr_test_classes.hh",
    ],
    includes = ["test/integration/implptr"],
    deps = ["ign_utils"],
)

cc_test(
    name = "ImplPtr_TEST",
    srcs = ["test/integration/implptr/ImplPtr_TEST.cc"],
    deps = [
        ":implptr_test_classes",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

add_lint_tests()
