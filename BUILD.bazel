load(
    "@gz//bazel/skylark:build_defs.bzl",
    "GZ_FEATURES",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "gz_configure_header",
    "gz_export_header",
    "gz_include_header",
)
load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = [GZ_ROOT + "utils:license"],
    default_visibility = GZ_VISIBILITY,
    features = GZ_FEATURES,
)

license(
    name = "license",
    package_name = "gz-utils",
)

licenses(["notice"])

exports_files(["LICENSE"])

gz_configure_header(
    name = "config",
    src = "include/gz/utils/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    package = "utils",
)

gz_export_header(
    name = "include/gz/utils/Export.hh",
    export_base = "GZ_UTILS",
    lib_name = "gz-utils",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/gz/utils/*.hh",
    "include/gz/utils/detail/*.hh",
    "include/gz/utils/detail/*.h",
])

gz_include_header(
    name = "utilshh_genrule",
    out = "include/gz/utils.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/utils/Export.hh",
        "include/gz/utils/config.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/utils/config.hh",
    "include/gz/utils/Export.hh",
    "include/gz/utils.hh",
]

cc_library(
    name = "utils",
    srcs = ["src/Environment.cc"],
    hdrs = public_headers,
    copts = ["-fexceptions"],
    includes = ["include"],
)

# Tests
cc_library(
    name = "implptr_test_classes",
    srcs = [
        "test/integration/implptr/implptr_test_classes.cc",
    ],
    hdrs = [
        "test/integration/implptr/implptr_test_classes.hh",
    ],
    includes = ["test/integration/implptr"],
    deps = [":utils"],
)

cc_test(
    name = "ImplPtr_TEST",
    size = "small",
    srcs = ["test/integration/implptr/ImplPtr_TEST.cc"],
    deps = [
        ":implptr_test_classes",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

cc_test(
    name = "Environment_TEST",
    srcs = ["src/Environment_TEST.cc"],
    deps = [
        ":utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

cc_test(
    name = "NeverDestroyed_TEST",
    srcs = ["src/NeverDestroyed_TEST.cc"],
    copts = ["-fexceptions"],
    deps = [
        ":utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)
